plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'groovy'
    id "biz.aQute.bnd.builder" version "5.1.2"
    id "org.unbroken-dome.xjc" version "2.0.0"
    //TODO: spotless
}

group = 'de.interactive_instruments'

version = '2.0.3'

repositories {
    mavenCentral()
}

configurations {
    doclava
}

dependencies {
    implementation 'com.google.guava:guava:27.0-jre'
    implementation 'org.apache.ws.commons.schema:XmlSchema:1.4.7'
    implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '4.0.0'
    implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '4.0.1'

    compileOnly "org.immutables:value:2.8.4:annotations"
    annotationProcessor "org.immutables:value:2.8.4"

    testFixturesApi 'com.google.guava:guava:27.0-jre'

    testImplementation "junit:junit:4.12"
    testImplementation "org.xmlunit:xmlunit-core:2.5.1"
    testImplementation "org.xmlunit:xmlunit-matchers:2.5.1"
    testImplementation "com.shazam:shazamcrest:0.11"
    testImplementation 'com.greghaskins:spectrum:1.2.0'
    testImplementation "org.mockito:mockito-core:+"
    testImplementation("org.assertj:assertj-core:+")
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.4"

    doclava 'com.google.doclava:doclava:1.0.6'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileJava {
    File generatedSourceDir = new File(buildDir, 'generated/src/main/java/')
    sourceSets.main.java { srcDir generatedSourceDir }
    options.annotationProcessorGeneratedSourcesDirectory = generatedSourceDir
    outputs.dir(generatedSourceDir)
}

jar.manifest.attributes(
        '-exportcontents': 'de.interactive_instruments.*',
        '-removeheaders': 'Bnd-LastModified,Created-By,Tool,Private-Package',
        'Import-Package': '!javax.annotation.*,!org.immutables.value,!de.interactive_instruments.*,*',
        'Bundle-Name': name,
        'Bundle-SymbolicName': "${group}.${name.replaceAll('-', '.')}",
        'Bundle-Vendor': 'interactive-instruments GmbH',
        'Bundle-License': "http://www.apache.org/licenses/LICENSE-2.0"
)

test {
    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED", "standard_out", "standard_error"
    }
}

//license {
//    exclude "**/Immutable*"
//}

xjc {
    xjcVersion = '3.0'
    srcDirName = 'resources'
}

sourceSets {
    xjc {
        java {}
    }
    main {
        compileClasspath += sourceSets.xjc.output
        runtimeClasspath += sourceSets.xjc.output
        xjcSchema.include 'XtraServer_Mapping.xsd'
        xjcBinding.include 'bindings.xjb'
    }
    test {
        xjcSchema.exclude '**/*'
    }
}

configurations {
    xjcImplementation.extendsFrom(implementation)
}

xjcGenerate {
    outputDirectory = file('src/xjc/java')
    targetPackage = 'de.interactive_instruments.xtraserver.config.schema'
}

javadoc {
    exclude 'de/interactive_instruments/xtraserver/config/schema/*'
    options.doclet = "com.google.doclava.Doclava"
    options.docletpath = configurations.doclava.files.asType(List)
    title = null

}
tasks.javadoc.dependsOn configurations.doclava

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.xjc.allSource
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

//license {
//    header file('./gradle/license-header')
//    strictCheck true
//    includes(['**/*.java'])
//    ext.year = Calendar.getInstance().get(Calendar.YEAR)
//    ext.name = "interactive instruments GmbH"
//}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-releases/"
            credentials {
                username project.findProperty('deployUser') ?: ''
                password project.findProperty('deployPassword') ?: ''
            }
        }
    }
}

plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'groovy'
    id "biz.aQute.bnd.builder" version "5.1.2"
    id "org.unbroken-dome.xjc" version "1.4.3"
    //TODO: spotless
}

group = 'de.interactive_instruments'

version = '1.0.12'

repositories {
    mavenCentral()
}

configurations {
    doclava
}

dependencies {
    api 'com.google.guava:guava:27.0-jre'
    //TODO: is this already in hale, or was it included by us?
    api 'org.apache.ws.commons.schema:XmlSchema:1.4.7'
    //TODO: why
    //compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'

    //TODO: is this included in hale?
    //implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.12'
    //implementation group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.2.11'
    //implementation group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.2.11'

    compileOnly         "org.immutables:value:2.8.4:annotations"
    annotationProcessor "org.immutables:value:2.8.4"

    testImplementation "junit:junit:4.12"
    testImplementation "org.xmlunit:xmlunit-core:2.5.1"
    testImplementation "org.xmlunit:xmlunit-matchers:2.5.1"
    testImplementation "com.shazam:shazamcrest:0.11"
    testImplementation 'com.greghaskins:spectrum:1.2.0'
    testImplementation "org.mockito:mockito-core:+"
    testImplementation("org.assertj:assertj-core:+")
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.4"

    doclava 'com.google.doclava:doclava:1.0.6'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileJava {
    File generatedSourceDir = new File(buildDir, 'generated/src/main/java/')
    sourceSets.main.java { srcDir generatedSourceDir }
    options.annotationProcessorGeneratedSourcesDirectory = generatedSourceDir
    outputs.dir(generatedSourceDir)
}

jar.manifest.attributes(
        '-exportcontents': 'de.interactive_instruments.*',
        '-removeheaders': 'Bnd-LastModified,Created-By,Tool,Private-Package',
        'Import-Package': '!javax.annotation.*,!org.immutables.value,!de.interactive_instruments.*,*',
        'Bundle-Name': name,
        'Bundle-SymbolicName': "${group}.${name.replaceAll('-','.')}",
        'Bundle-Vendor': 'interactive-instruments GmbH',
        'Bundle-License': "http://www.apache.org/licenses/LICENSE-2.0"
)

test {
    testLogging {
        events "PASSED", "STARTED", "FAILED", "SKIPPED", "standard_out", "standard_error"
    }
}

//license {
//    exclude "**/Immutable*"
//}

xjc {
    includeInMainCompilation = false
}

xjcGenerate {
    source = fileTree('src/main/resources/XtraServer_Mapping.xsd')
    bindingFiles = fileTree('src/main/resources/bindings.xjb')
    outputDirectory = file('src/main/java')
    targetPackage = 'de.interactive_instruments.xtraserver.config.schema'
}

javadoc {
    exclude 'de/interactive_instruments/xtraserver/config/schema/*'
    options.doclet = "com.google.doclava.Doclava"
    options.docletpath = configurations.doclava.files.asType(List)
    title = null

}
tasks.javadoc.dependsOn configurations.doclava

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

//license {
//    header file('./gradle/license-header')
//    strictCheck true
//    includes(['**/*.java'])
//    ext.year = Calendar.getInstance().get(Calendar.YEAR)
//    ext.name = "interactive instruments GmbH"
//}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-releases/"
            credentials {
                username project.findProperty('deployUser') ?: ''
                password project.findProperty('deployPassword') ?: ''
            }
        }
    }
}
